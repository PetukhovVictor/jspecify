// run `./gradlew publishToMavenLocal` to install artifact to your Local Maven Repository

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = 'org.jspecify.annotations'
                name = 'jspecify'
                description = 'An artifact of well-named and well-specified annotations to power static analysis checks'
                url = 'https://jspecify.org/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:jspecify/jspecify.git'
                    developerConnection = 'scm:git:git@github.com:jspecify/jspecify.git'
                    url = 'https://github.com/jspecify/jspecify/'
                }
            }
        }
    }
}

// compile module-info.java with Java9, and others with Java8
// https://github.com/java9-modularity/gradle-modules-plugin#separate-compilation-of-module-infojava
modularity.mixedJavaRelease 8

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
